# Copyright 2023 The Lynx Authors. All rights reserved.
# 

import("//build/apple/templates/darwin_binary_bundle.gni")
import("//build/config/ios/bundle_identifier_prefix.gni")
import("//build/config/mac/mac_sdk.gni")

# Template to create a Mac executable application bundle.
#
# Arguments
#
#     bundle_identifier_prefix:(optional)
#          prefix for the bundle identifier (the full identifier will be defined
#          to $bundle_identifier_prefix.$output_name); if unset will defaults to
#          default_bundle_identifier_prefix
#
#     info_plist: (optional) 
#         string, path to the Info.plist file that will be used for
#         the bundle.
#
#     output_name: (optional) 
#         name of the bundle without the extension; defaults to $target_name
#

template("mac_app_bundle") {
  _output_name = target_name
  if (defined(invoker.output_name)) {
    _output_name = invoker.output_name
  }

  _bundle_identifier_prefix = default_bundle_identifier_prefix
  if (defined(invoker.bundle_identifier_prefix)) {
    _bundle_identifier_prefix = invoker.bundle_identifier_prefix
  }

  _bundle_identifier = "$_bundle_identifier_prefix.$_output_name"

  _app_prefix_target = target_name + "_app_prefix"
  _app_prefix_output = "$target_out_dir/$_app_prefix_target/app_prefix.json"

  action(_app_prefix_target) {
    script = "//build/config/ios/scripts/find_app_identifier_prefix.py"
    sources = []
    outputs = [ _app_prefix_output ]
    args = [
      "-b=" + _bundle_identifier,
      "-o=" + rebase_path(_app_prefix_output, root_build_dir),
    ]
  }

  _executable_target = target_name + "_executable"
  _executable_bundle = target_name + "_executable_bundle"

  executable(_executable_target) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "bundle_extension",
                             "bundle_identifier_prefix",
                             "bundle_type",
                             "configs",
                             "display_name",
                             "info_plist",
                             "output_name",
                             "public_headers",
                           ])

    output_extension = ""
    output_name = _output_name
    output_prefix_override = true
    output_dir = "$target_out_dir/$_executable_target"

    if (!defined(configs)) {
      configs = []
    }
    if (defined(invoker.configs)) {
      configs += invoker.configs
    }
    
    if (!defined(exclude_configs)) {
      exclude_configs = []
    }
    configs -= exclude_configs
  }

  bundle_data(_executable_bundle) {
    public_deps = [ ":$_executable_target" ]
    sources = [ "$target_out_dir/$_executable_target/$_output_name" ]
    outputs = [ "{{bundle_executable_dir}}/{{source_file_part}}" ]
  }

  darwin_binary_bundle(target_name) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "bundle_extension",
                             "bundle_identifier_prefix",
                             "bundle_type",
                             "deps",
                             "output_name",
                             "public_deps",
                             "public_headers",
                           ])

    output_name = _output_name
    product_type = "com.apple.product-type.application"
    deployment_target = mac_deployment_target

    bundle_identifier = _bundle_identifier
    bundle_extension = "app"
    bundle_type = "APPL"

    public_deps = [ ":$_executable_bundle" ]
  }

}